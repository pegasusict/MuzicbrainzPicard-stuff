$noop( â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„
  â„â„â„â„  About  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„
â„â„  Pegasus' Picard File Naming Script v0.1.1-dev                 2021-03-04  â„â„
â„   CopyLeft ğŸ„¯ 2021 Mattijs Snepvangers [pegasusict]                           â„
â„   based on rdswift's Filenaming script                                       â„
â„â„  License: GPLv3.0                                                          â„â„
  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„
â„â„      This script relies on inputs provided by the following plugins:       â„â„
â„â„    â‡’ Additional Artists Variables [AAV]                                    â„â„
  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$noop(â„â„â„â„ CHANGELOG â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
0.1.1-dev [FIX] Fixed a bug which malformed the trackartist name. credit: outsidecontext
          https://community.metabrainz.org/t/artist-case-mismatch-in-filenaming-script/522434/1
â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)


$noop(â„ User Settings  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_PaddedDiscNumMinLength,1)
$set(_PaddedTrackNumMinLength,2)
$set(_aTitleMaxLength,65)
$set(_tTitleMaxLength,65)
$set(_tFilenameMaxLength,120)
$set(_aTitleReleaseYear,)$noop(binary switch)
$set(_aTitleDisambig,1)$noop(binary switch)
$set(_aTitleLabel,)$noop(binary switch)
$set(_aTitleCatalog,)$noop(binary switch)
$noop(collate compilation tracks under "trackartist/[Compilations]/" - binary switch)
$set(_aCollateCompilations,1)
$noop(collate Singles' tracks under "trackartist/[Singles]/" - binary switch)
$set(_aCollateSingles,1)
$set(_aTitleCatalog,)$noop(binary switch)
$setmulti(_Prefixes,THE; The; the; A; a; AN; An; an; DE; De; de; EEN; Een; een; HET; Het; het; LE; Le; le)
$noop(â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)

$noop(â„ Constants  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_cScriptName,Pegasus' Picard File Naming Script)
$set(_cScriptVersion,0.1.0-alpha)
$set(_cScriptBuild,2021-03-04)
$set(_cScriptAuthor,Mattijs Snepvangers [pegasusict])
$set(_cScriptUrl,https://github.com/pegasusict/MuzicbrainzPicard-stuff)
$set(_cScriptLicense,GPLv3.0)
$set(_cSignature,Processed by MusicBrainz Picard at $datetime() using
 %_cScriptName% v%_cScriptVersion% build%_cScriptBuild%. %_cScriptUrl%)

$set(_cUnknownArtistID,125ec42a-7229-4250-afc5-e057484327fe)
$set(_cVariousArtistID,89ad4ac3-39f7-470e-963a-56509c546377)
$set(_cUnknownArtist,[Unknown Artist])$noop(â„â„â„ TODO: option to isolate from index folders â„â„â„)
$set(_cVariousArtist,[Various Artists])
$set(_cUnknownAlbum,[Unknown Album])
$set(_cNoTitle,[Unknown Title])
$set(_cClassical,[Classical])
$set(_cSoundtrack,[Soundtracks])
$set(_cCompilation,[Compilations])
$set(_cSingles,[Singles])
$set(_cOther,[Other])
$noop(â„  RegEx Constants  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_reCaseInsensitive,\(?i\))
$noop(â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)


$noop(â„ Setting sensible default values if AAV plugin or metadata is missing. â„â„)
$set(_nFAA,$if2(%_artists_album_all_std%,%albumartist%,%_cUnknownArtist%))
$set(_nPAA,$if2(%_artists_album_primary_std%,%albumartist%,%_cUnknownArtist%))
$set(_nFAAS,$if2(%_artists_album_all_sort%,%albumartistsort%,%_cUnknownArtist%))
$set(_nPAAS,$if2(%_artists_album_primary_sort%,%albumartistsort%,%_cUnknownArtist%))
$set(_nFAAPS,$if2(%_artists_album_all_sort_primary%,%albumartistsort%,%_cUnknownArtist%))
$set(_nPTA,$if2(%_artists_track_primary_cred%,%artist%,%_cUnknownArtist%))
$set(_nATA,%_artists_track_additional_cred%)
$set(_nFTA,$if2(%_artists_track_all_cred%,%artist%,%_cUnknownArtist%))
$set(_nAN,$if2(%album%,%_cUnknownAlbum%))
$set(_nANT,$if2(%album%,%_cUnknownAlbum%))
$set(_nTNT,$if2(%title%,%_cNoTitle%))


$noop(â„ Initialize Working Variables  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_nMedia,%media%)
$set(_nTotalDiscs,$if2(%totaldiscs%,1))
$set(_nDiscNum,$if2(%discnumber%,1))
$set(_nTotalTracks,$if2(%totaltracks%,1))
$set(_nTrackNum,$if2(%tracknumber%,1))
$set(_nAlbumArtistID,$if2(%musicbrainz_albumartistid%,%_kUnKnownArtistID%))
$set(_nIsCompilation,$if($or($gt(%_artists_album_all_count%,1),%compilation%),1,))
$set(_nCCIC,$if($and(%_aCollateCompilations%,%_nIsCompilation%),1,))
$set(_nIsSingle,$if($or($lte(%_nTotaltracks%,2),$eq(%_primaryreleasetype%,single)),1,))
$set(_nCSIS,$if($and(%_aCollateSingles%,%_nIsSingle%),1,))
$if(%_nCCIC%,$set(_nANT,%_cCompilation%),)
$if(%_nCSIS%,$set(_nANT,%_cSingle%),)
$set(_nInitial,$upper($firstalphachar(%_nPAAS%,0-9))/)
$set(_nYear,[$if($or(%_nCCIC%,%_nCSIS%),0000,$left($if2(%originaldate%,%originalyear%,%date%,0000),4))])
$set(_nBR,[$rreplace(%_bitrate%,\\.\\d*\$,)])
$set(_nLen,[%_length%])

$noop( â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„
â„â„  If standardized primary album artist is different from credited primary    â„â„
â„   any track artist [other than prefix] show in track file name. Otherwise,     â„
â„â„  show additional credited track artists in track file name.                 â„â„
  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_tAlbumArtist,$lower($swapprefix(%_nPAA%, %_Prefixes%)))
$set(_tTrackArtist,$swapprefix(%_nPTA%, %_Prefixes%))
$if($eq(%_tAlbumArtist%,$lower(%_tTrackArtist%)),
$set(_nFeat,$if(%_nATA%, [feat. %_nATA%],)),$set(_nFeat, [%_nFTA%]))

$noop(â„â„  Calculate and apply padding for disc and track numbers. â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$if($not($or(%_nCCIC%,%_nCSIS%)),
$if($gt(%_nTotalDiscs%,1),
$set(_TotalDiscNumberLength,$len($if2(%totaldiscs%,1)))
$set(_DiscPadLength,$if($gt(%_TotalDiscNumberLength%,%_PaddedDiscNumMinLength%),
%_TotalDiscNumberLength%,%_PaddedDiscNumMinLength%))
$set(_PaddedDiscNum,$num($if2(%discnumber%,1),%_DiscPadLength%)-),$noop()
)
$set(_TotalTrackNumberLength,$len($if2(%totaltracks%,1)))
$set(_TrackPadLength,$if($gt(%_TotalTrackNumberLength%,%_PaddedTrackNumMinLength%),
%_TotalTrackNumberLength%,%_PaddedTrackNumMinLength%))
$set(_nTNum,%_PaddedDiscNum%$num($if2(%tracknumber%,1),%_TrackPadLength%))
,)
$noop( â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„
â„â„  Add disambiguation, release year, label and catalog number to the         â„â„
â„â„  album title information if available and enabled in "User Settings".      â„â„
  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_nDisambig,$if($and(%_releasecomment%,%_aTitleDisambig%), \(%_releasecomment%\),))
$set(_nTitleExtra,)
$if(%_aTitleLabel%,$if(%label%,$setmulti(_temp,%label%)$set(_nTitleExtra,$getmulti(%_temp%,0))))
$if(%_aTitleCatalog%,$if(%catalognumber%,$setmulti(_temp,%catalognumber%)$set(_nTitleExtra,$trim(%_nTitleExtra% $getmulti(%_temp%,0)))))
$if(%_aTitleReleaseYear%,$if(%date%,$set(_temp,[$left(%date%,4)])
$if($ne([%_temp%],%_nYear%),$if(%_nTitleExtra%,$set(_nTitleExtra,%_nTitleExtra%\,))$set(_nTitleExtra,$trim(%_nTitleExtra% %_temp%)))))
$if(%_nTitleExtra%,$set(_nTitleExtra, [%_nTitleExtra%]))
$set(_nANT,%_nANT%%_nDisambig%%_nTitleExtra%)

$noop(â„  Remove any illegal characters  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_nANT,$replace_forbidden(%_nANT%))
$set(_nTNT,$replace_forbidden(%_nTNT%))

$noop( â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„
â„â„  Trim the album and track names used to create directories and             â„â„
â„   tracks if they are longer than the maximum lengths set in the              â„
â„â„  "User Settings" section.                                                  â„â„
  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$if($gt($len(%_nANT%),%_aTitleMaxLength%),$set(_nANT,$left(%_nANT%,$sub(%_aTitleMaxLength%,1))â€¦))
$if($gt($len(%_nTNT%),%_tTitleMaxLength%),$set(_nTNT,$left(%_nTNT%,$sub(%_tTitleMaxLength%,1))â€¦))

$noop(â„  Set File Path  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$if($eq_any(%musicbrainz_albumartistid%,%_cVariousArtistID%,%_cUnknownArtistID%),
$set(_nInitial,$upper($firstalphachar(%_nPTA%,0-9))/)$set(_nArtist,%_nPTA%),
$set(_nArtist,%_nPAAS%)
)
$set(_nFilePath,%_nInitial%/%_nArtist%/%_nYear% %_nANT%/)

$noop(â„  Set FileName  â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(_nFileName,$if($not($or(%_nCCIC%,%_nCSIS%)),%_nTNum%. ,)%_tTrackArtist% - %_nTNT%)
$set(_tFilenameMaxLength,$sub(%_tFilenameMaxLength%,17))
$if($gt($len(%_nFileName%),%_tFilenameMaxLength%),$set(_nFileName,$left(%_nFileName%,%_tFilenameMaxLength%)â€¦))
$set(_nFileName,%_nFileName% %_nLen% %_nBR%)

$noop(â„  Result â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„â„)
$set(ProcessedBy,%_cSignature%)
$set(OutputPath,$rreplace(%_nFilePath%,[?*:\\_]+,_))
$set(OutputFileName,$rreplace(%_nFileName%,[?*:\\_]+,_))

%OutputPath%/%OutputFileName%
